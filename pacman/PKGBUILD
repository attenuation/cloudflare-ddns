pkgname=cloudflare-ddns
pkgver=1.1.1
pkgrel=1
pkgdesc="Cloudflare DDNS client written by golang"
arch=('x86_64')
url="https://github.com/attenuation/cloudflare-ddns"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
backup=(etc/conf.d/cloudflare-ddns)
source=("$pkgname-$pkgver.tar.gz::https://github.com/attenuation/cloudflare-ddns/archive/v$pkgver.tar.gz")
sha512sums=('b3fb40c706b2d17bae835be8042cd129395aa6920c06f04cc63d22e57ed0c17f12d7333349127a80c84284509e812899533945cac31a15f287a24a0f9596f764')

build() {
  cd "$pkgname-$pkgver"
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 systemd/cloudflare-ddns.service "$pkgdir"/usr/lib/systemd/system/cloudflare-ddns.service
  install -Dm644 conf.d/cloudflare-ddns -t "$pkgdir"/etc/conf.d/
  install -dm755 "$pkgdir"/usr/bin
  install -Dm755 cloudflare-ddns -t "$pkgdir"/usr/bin/
}